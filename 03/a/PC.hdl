// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=outreg, out=outinc);
    Mux4Way16(a=false, b=in, c=outinc, d=in, sel[0]=load, sel[1]=inc,
              out=outmux1); 
    Mux16(b=false, a=outmux1, sel=reset, out=outmux2);
    Or(a=load, b=inc, out=outor1);
    Or(a=outor1, b=reset, out=outor2);
    Register(in=outmux2, load=outor2, out=out, out=outreg);

}
